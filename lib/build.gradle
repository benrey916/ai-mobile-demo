plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.whispercpp'
    compileSdk 36

    defaultConfig {
        minSdk 26
        targetSdk 36

        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
        }
        externalNativeBuild {
            cmake {
                // Reproducible build flags - ADDED
                arguments "-DCMAKE_BUILD_TYPE=Release"

                cppFlags "-ffile-prefix-map=\${projectDir}=."
                cFlags "-ffile-prefix-map=\${projectDir}=."

                // When set, builds whisper.android against the version located
                // at GGML_HOME instead of the copy bundled with whisper.cpp.
                if (
                        project.hasProperty('GGML_HOME') &&
                                project.findProperty('GGML_CLBLAST') == 'ON'
                ) {
                    // Turning on CLBlast requires GGML_HOME
                    arguments "-DGGML_HOME=${project.property('GGML_HOME')}",
                            "-DGGML_CLBLAST=ON",
                            "-DOPENCL_LIB=${project.property('OPENCL_LIB')}",
                            "-DCLBLAST_HOME=${project.property('CLBLAST_HOME')}",
                            "-DOPENCL_ROOT=${project.property('OPENCL_ROOT')}",
                            "-DCMAKE_FIND_ROOT_PATH_MODE_INCLUDE=BOTH",
                            "-DCMAKE_FIND_ROOT_PATH_MODE_LIBRARY=BOTH",
                            "-DCMAKE_BUILD_TYPE=Release"  // ADDED
                } else if (project.hasProperty('GGML_HOME')) {
                    arguments "-DGGML_HOME=${project.property('GGML_HOME')}",
                            "-DCMAKE_BUILD_TYPE=Release"  // ADDED
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    externalNativeBuild {
        cmake {
            path = file("src/main/jni/whisper/CMakeLists.txt")
        }
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    ndkVersion '27.0.12077973'
}

// Reproducible build configuration - ADDED
tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

dependencies {
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.12.0'
}
